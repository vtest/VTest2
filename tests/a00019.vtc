varnishtest "vtc v_* macros when the listen address is UDS"

varnish v1 -arg "-a ${tmpdir}/v1.sock -b '${bad_backend}'" -start

varnish v1 -syntax 4.0 -errvcl {Compiled VCL version (4.0) not supported.} {
	backend default { .host = "${bad_ip}"; }
}

varnish v1 -syntax 4.0 -errvcl \
	{Unix socket backends only supported in VCL4.1 and higher.} \
	{backend default { .path = "${tmpdir}/v1.sock"; }}

varnish v2 -vcl {
	backend default { .host = "${bad_ip}"; }

	sub vcl_recv {
		return(synth(200));
	}

	sub vcl_synth {
		set resp.http.addr = "${v1_addr}";
		set resp.http.port = "${v1_port}";
		set resp.http.sock = "${v1_sock}";
	}
} -start

client c1 -connect ${v2_sock} {
	txreq
	rxresp
	expect resp.http.addr == "${tmpdir}/v1.sock"
	expect resp.http.port == "-"
	expect resp.http.sock == "${tmpdir}/v1.sock -"
} -run
